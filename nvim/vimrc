set encoding=utf-8
scriptencoding utf-8

" Appearance & color scheme
" -------------------------
syntax enable

let g:edge_transparent_background=1
colorscheme edge

set nofoldenable

" use mouse in terminal vim
set mouse=a

" use system clipboard for default copy-and-paste register
set clipboard=unnamedplus

" line numbers
set ruler " show line numbers
set relativenumber " relative line numbers
set number " shows current line as absolute line number (Vim 7.4+)
set cursorline " highlights current line

" open splits below/right of current area, like a sane person
set splitbelow
set splitright

" keep 5 lines above/below the cursor when scrolling
set scrolloff=5

" don't be annoying
set noerrorbells
set novisualbell

" show powerline all the time & hide duplicate mode
set laststatus=2
set noshowmode

" gui options
set guifont=Menlo:h12
set guioptions-=m
set guioptions-=t
set guioptions-=r
set guioptions-=e

" hide splash screen
set shortmess+=I


" Ale
let g:ale_sign_warning = '▲'
let g:ale_sign_error = '✗'
highlight clear ALEErrorSign
highlight clear ALEWarningSign
highlight clear SignColumn

" Git gutter
let g:gitgutter_sign_added = '·'
let g:gitgutter_sign_modified = '·'
let g:gitgutter_sign_modified_removed = '≃'

" Git diff/merge
set diffopt+=vertical

" Aliases
" -----------------
" Set leader to ',' since its easier to type
let g:mapleader = ','

" shortcut to get out of insert mode.
imap jj <Esc>

" shortcut for opening tab
nnoremap <silent> tt :tabnew<CR>

" Go to tab by number
noremap <leader>1 1gt
noremap <leader>2 2gt
noremap <leader>3 3gt
noremap <leader>4 4gt
noremap <leader>5 5gt
noremap <leader>6 6gt
noremap <leader>7 7gt
noremap <leader>8 8gt
noremap <leader>9 9gt
noremap <leader>0 :tablast<cr>

" shortcuts for creating splits
nnoremap <silent> vv <C-w>v
nnoremap <silent> ss <C-w>s

" shortcuts to switch between splits
nnoremap sh <C-w>h
nnoremap sl <C-w>l
nnoremap sj <C-w>j
nnoremap sk <C-w>k

" shortcut to hide search highlights
nmap <leader>h :nohlsearch<cr>

" build
augroup builds
  autocmd FileType go nmap <leader>b  <Plug>(go-build)
  autocmd FileType javascript nmap <leader>b  :!/usr/local/bin/node %<cr>
augroup END

" run go tests
augroup tests
  autocmd FileType go nmap <leader>t  <Plug>(go-test)
augroup END

" distraction free mode
nmap <leader>m :Goyo<cr>

" Command-S for fun
map <C-s> :w<cr>

" Quick file-switching via FZF
map <D-p> :Files!<cr>
map <C-p> :Files!<cr>
map <C-o> :Recents!<cr>
map <C-b> :Buffers<cr>
map <C-d> :BTags<cr>
command! -bang Recents call fzf#vim#history(fzf#vim#with_preview(), <bang>0)
command! -bang -nargs=? -complete=dir Files
      \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)
let g:fzf_history_dir = '~/.local/share/fzf-history'

" NERDTree
map <C-n> :call ToggleNERDTreeFind()<CR>

function! ToggleNERDTreeFind()
  if g:NERDTree.IsOpen()
    execute ':NERDTreeClose'
  else
    execute ':NERDTreeFind'
  endif
endfunction

" view git changes for hunk
nmap <leader>g <Plug>GitGutterPreviewHunk

" reveal in finder/nautilus
function! s:Reveal()
  if has('macunix')
    let l:fcommand = "open -R"
    let l:dcommand = "open"
  else
    let l:fcommand = "nautilus -s"
    let l:dcommand = "nautilus"
  endif

  if filereadable(expand("%"))
    let l:command = l:fcommand . " " . shellescape("%")
  elseif getftype(expand("%:p:h")) == "dir"
    let l:command = l:dcommand . " " . shellescape("%") . ":p:h"
  else
    let l:command = l:dcommand " ."
  endif

  execute ":silent! !" . l:command . "&"
  redraw!
endfunction

command! Reveal call <SID>Reveal()

" Code Completion
" ---------------
set omnifunc=syntaxcomplete#Complete

set completeopt=longest,menuone

" fix <Enter> behavior when autocomplete is open.
inoremap <expr> <CR> "\<C-g>u\<CR>"

" UltiSnips
let g:UltiSnipsExpandTrigger='<C-S>'
let g:UltiSnipsJumpForwardTrigger='<C-S>'
let g:UltiSnipsJumpBackwardTrigger='<C-D>'


" Delimitmate
" -----------

" let delimitmate expand carriage returns with correct indentation
let g:delimitMate_expand_cr=1


" Opening/closing files
" ---------------------

set autowrite

" Remember last location in file (except git commit prompt)
augroup remember
  au BufReadPost * if &filetype !~ '^git\c' && line("'\"") > 0 && line("'\"") <= line("$")
      \| exe "normal! g`\"" | endif
augroup END

" automatically read a file that has changed on disk
set autoread

" write without touching timestamp
function! WriteQuietly()
    let mtime = system("gdate -d @`gstat -c %Y ".shellescape(expand('%:p')) . "`")
    write
    call system("gtouch --date='".mtime."' ".shellescape(expand('%:p')))
endfunction

command WriteQuietly :call WriteQuietly()

" Filetypes & syntax highlighting
" ----------------------------------
syntax on
filetype off
filetype indent on
filetype plugin on

set showmatch " show matching brackets

" set default indentation settings
set expandtab
set tabstop=2
set shiftwidth=2
set backspace=2
set autoindent
set smartindent
set nocindent

" custom syntax highlighting:
au BufNewFile,BufRead *.bu,*.fcc set filetype=yaml
au BufNewFile,BufRead *.env.example set ft=sh
au BufNewFile,BufRead Brewfile set ft=sh

" go:
let g:go_fmt_command = 'goimports'
let g:go_highlight_types = 1
let g:go_highlight_fields = 1
let g:go_highlight_functions = 1
let g:go_highlight_function_calls = 1

" comment settings for terraform/hcl
autocmd FileType terraform setlocal commentstring=#\ %s
autocmd FileType vcl setlocal commentstring=#\ %s

" markdown:
let g:vim_markdown_fenced_languages = ['bash=sh', 'js=javascript']
let g:vim_markdown_math = 1
let g:goyo_width = 100

let g:pencil#wrapModeDefault = 'soft'
let g:pencil#conceallevel = 0

augroup pencil
  autocmd!
  autocmd FileType markdown     call pencil#init()
augroup END

augroup markdown_tags
  au FileType markdown syntax match Label /\#[a-z]*/
augroup end

" goyo
function! s:goyo_enter()
  let b:quitting = 0
  let b:quitting_bang = 0
  autocmd QuitPre <buffer> let b:quitting = 1
  cabbrev <buffer> q! let b:quitting_bang = 1 <bar> q! ...
endfunction

function! s:goyo_leave()
  " Quit Vim if this is the only remaining buffer
  if b:quitting && len(filter(range(1, bufnr('$')), 'buflisted(v:val)')) == 1
    if b:quitting_bang
      qa!
    else
      qa
    endif
  endif
endfunction

autocmd! User GoyoEnter nested call <SID>goyo_enter()
autocmd! User GoyoLeave nested call <SID>goyo_leave()

" enable JSDoc syntax highlighting
let g:javascript_plugin_jsdoc = 1

" use tabs in makefiles
augroup makefiles
  au FileType make setlocal noexpandtab
augroup END

" highlight docblocks in php
function! PhpSyntaxOverride()
  hi! def link phpDocTags  phpDefine
  hi! def link phpDocParam phpType
endfunction

augroup phpSyntaxOverride
  autocmd!
  autocmd FileType php call PhpSyntaxOverride()
augroup END


" Formatting
" ----------
let g:prettier#autoformat = 0
augroup prettier
  autocmd BufWritePre *.js,*.css,*.scss,*.graphql,*.md PrettierAsync
augroup END

let g:terraform_fmt_on_save=1

" Search
" ------
set incsearch " highlight matches as we search
set hlsearch " keep search highlights after search
set ignorecase " ignore case when searching
set smartcase " ...but make it case-sensitive if not all lowercase


" Swap, backup, and undo
" ----------------------
" set backupdir=~/.vim/backups//
" set undodir=~/.vim/undodir//

" disable swap file
set noswapfile

set undofile " enables peristent undo (keeps history on file reload)
set undolevels=1000 "maximum number of changes that can be undone
set undoreload=10000 "maximum number lines to save for undo on a buffer reload

" NeoVim
" ------

if has('nvim')
  " Required for operations modifying multiple buffers like rename.
  set hidden

  let g:LanguageClient_autoStart = 1
  let g:LanguageClient_serverCommands = {
        \ 'rust': ['rustup', 'run', 'nightly', 'rls'],
        \ 'typescript': ['javascript-typescript-stdio'],
        \ 'javascript.jsx': ['javascript-typescript-stdio'],
        \ 'python': ['pyls'],
        \ }

  map <C-k> :call LanguageClient_textDocument_documentSymbol()<CR>
  nnoremap <silent> K :call LanguageClient_textDocument_hover()<CR>
  nnoremap <silent> gd :call LanguageClient_textDocument_definition()<CR>
  nnoremap <leader>r :call LanguageClient_textDocument_rename()<CR>

  " deoplete
  let g:deoplete#enable_at_startup = 1

  " use <TAB> to complete (https://git.io/vbO5a)
  inoremap <silent><expr> <TAB>
        \ pumvisible() ? "\<C-n>" :
        \ <SID>check_back_space() ? "\<TAB>" :
        \ deoplete#mappings#manual_complete()
  function! s:check_back_space() abort "{{{
    let l:col = col('.') - 1
    return !l:col || getline('.')[l:col - 1] =~? '\s'
  endfunction"}}}
endif

" italicize code comments... needs to be near the end!
highlight Comment cterm=italic
